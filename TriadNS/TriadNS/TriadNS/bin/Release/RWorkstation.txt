routine RWorkstation[Real T1,T2](InOut Con)
		initial
			integer Mode:=0; //ожидание (1-работа)
			integer NodeCount; //число рабочих станций в сети
			integer MyNumber:=-1; //свой порядковый номер
			array[4] of real  Req;
			real delay:=0;
		endi
		event;
			//Print message+ToStr(Mode);
			if message="Hello" then
				schedule Answer in 0;
			else
				if Mode=10 then
					Req:=StrToRealArray(message);
					if Req[0]=MyNumber then
						delay := SystemTime - Req[3];
						Print "Recieved message "+ToStr(Req[2])+" from "+ToStr(Req[1]) + ". Delay - " + ToStr(delay);
					else
						Print "!!!!!Мне пришло чужое сообщение! (" + message + ")";
					endif;
				endif;
				if Mode=1 then
					Print "Starting";	
					Mode:=10;
					NodeCount:=StrToInt(message);
					Schedule Request in T1+(real)((integer)(RandomRealIn(0, T2)*10000))/10000;
				endif;

				if Mode=0 then
					Mode:=1;
					MyNumber:=StrToInt(message);
					Print "Мой номер - "+message;
				endif;
			endif
		ende
		event Answer;
			out "Host" through Con;
		ende

		event Request;
			Req[0]:=RandomIn(0, NodeCount-1);
			Req[1]:=MyNumber;
			Req[2]:=RandomIn(0, 1000);
			Req[3]:=SystemTime;
			Print ToStr(MyNumber) +" sending "+ ToStr(Req[2])+" to "+ToStr(Req[0]);
			out RealArrayToStr(Req) through Con;
			Schedule Request in T1+(real)((integer)(RandomRealIn(0, T2)*10000))/10000;
		ende
	endrout